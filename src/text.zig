const std = @import("std");
const c = @import("c.zig").c;
const Font = @import("font.zig").Font;
const Canvas = @import("render.zig").Canvas;
const Color = @import("color.zig").Color;

fn utf8ToCodepoints(text: []const u8, buf: []u32) usize {
    var i: usize = 0;
    var it = (std.unicode.Utf8View.init(text) catch return 0).iterator();
    while (it.nextCodepoint()) |cp| : (i += 1) {
        if (i >= buf.len) return i;
        buf[i] = cp;
    }
    return i;
}

fn shape(font: *Font, text: []const u8, count: *c_uint) struct { [*c]c.hb_glyph_info_t, [*c]c.hb_glyph_position_t } {
    var buf: [256]u32 = undefined;
    const len = utf8ToCodepoints(text, &buf);
    c.hb_buffer_reset(font.hb_buf);
    if (len > 0) {
        c.hb_buffer_add_utf32(font.hb_buf, &buf, @intCast(len), 0, @intCast(len));
    }
    c.hb_buffer_set_direction(font.hb_buf, c.HB_DIRECTION_LTR);
    c.hb_buffer_set_script(font.hb_buf, c.HB_SCRIPT_LATIN);
    c.hb_buffer_set_language(font.hb_buf, c.hb_language_from_string("en", 2));
    c.hb_shape(font.hb_font, font.hb_buf, null, 0);
    const info = c.hb_buffer_get_glyph_infos(font.hb_buf, count);
    const pos = c.hb_buffer_get_glyph_positions(font.hb_buf, count);
    return .{ info, pos };
}

pub fn renderText(canvas: *Canvas, font: *Font, text: []const u8, x: i32, y: i32, color: Color) void {
    if (color.a == 0 or text.len == 0) return;

    var glyph_count: c_uint = 0;
    const info, const pos = shape(font, text, &glyph_count);
    if (glyph_count == 0) return;

    var pen_x: i32 = x;
    const baseline_y: i32 = y;

    var i: u32 = 0;
    while (i < glyph_count) : (i += 1) {
        const gi = info[i];
        const gp = pos[i];
        const x_offset: i32 = @intCast(gp.x_offset >> 6);
        const y_offset: i32 = @intCast(gp.y_offset >> 6);
        const x_advance: i32 = @intCast(gp.x_advance >> 6);
        const glyph = font.getGlyph(gi.codepoint) catch {
            pen_x += x_advance;
            continue;
        };

        const dst_x = pen_x + x_offset + glyph.left;
        const dst_y = baseline_y + y_offset - glyph.top;

        canvas.blitGray(dst_x, dst_y, @intCast(glyph.width), @intCast(glyph.height), glyph.pitch, glyph.buf, color);

        pen_x += x_advance;
    }
}

pub fn textWidth(font: *Font, text: []const u8) i32 {
    if (text.len == 0) return 0;

    var glyph_count: c_uint = 0;
    const info_discard, const pos = shape(font, text, &glyph_count);
    _ = info_discard;
    if (glyph_count == 0) return 0;

    var total: i32 = 0;
    var i: u32 = 0;
    while (i < glyph_count) : (i += 1) {
        total += @as(i32, @intCast(pos[i].x_advance >> 6));
    }
    return total;
}
